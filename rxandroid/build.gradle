apply plugin: 'java'
apply plugin: 'net.ltgt.apt'
apply plugin: 'co.doppl.gradle'
apply plugin: 'maven-publish'

sourceCompatibility = 1.7
targetCompatibility = 1.7

def dopplCoreVersion = '0.7.1'
def dopplRxJavaVersion = '1.2.1.1'

dependencies {
    compile 'junit:junit:4.11'

    provided "co.doppl.lib:androidbase:$dopplCoreVersion"
    doppl "co.doppl.lib:androidbase:$dopplCoreVersion:doppl"

    testCompile "co.doppl.lib:androidbasetest:$dopplCoreVersion"
    testDoppl "co.doppl.lib:androidbasetest:$dopplCoreVersion:doppl"

    compile "io.reactivex:rxjava:1.2.1"
    doppl "co.doppl.io.reactivex:rxjava:$dopplRxJavaVersion:doppl"

    testApt "co.doppl.lib:testapt:$dopplCoreVersion"
}

group = 'co.doppl.io.reactivex'
version = dopplRxJavaVersion

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact(dopplArchive) {
                classifier 'doppl'
                extension 'jar'
            }
            pom {
                withXml {
                    asNode().dependencies.dependency.find {
                        it.artifactId.text() == "rxjava"
                    }.version[0].value = dopplRxJavaVersion
                    asNode().dependencies.dependency.find {
                        it.artifactId.text() == "rxjava"
                    }.groupId[0].value = "co.doppl.io.reactivex"
                }
            }
        }
    }
    repositories {
        maven {
            url dopplMavenDeploy
        }
    }
}

ext.bintrayGitUrl = 'https://github.com/doppllib/RxAndroid.git'

apply from: "$dopplSharedScriptHome/bintray.gradle"

test{
    enabled = false
}

dopplConfig {

    cycleFinderArgs '--whitelist', 'cycle_whitelist.txt'
    copyMainOutput "../iostest/dopplframework/main"
    copyTestOutput "../iostest/dopplframework/test"
    copyDependencies true

}